//Monastery Swordsman (d2mk2)

//Copy passive effects
COPY ~%passives%/hitdamage1.SPL~      ~override/%e1%.SPL~
COPY ~%passives%/nobowslingdart.SPL~  ~override/%e2%.SPL~
  LPF ALTER_EFFECT INT_VAR opcode=181 special=RESOLVE_STR_REF (@303) END


//Add Blazing Weapon
COPY ~%icons%/blazingweapon.BAM~ ~override/%s1%.BAM~
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@101) STR_VAR bam=~%s1%~ RET icon END
COPY ~%abilities%/blazingweapon.EFF~      ~override/%s1%.EFF~
COPY ~%abilities%/blazingweapon.SPL~      ~override/%s1%.SPL~
  SAY NAME1 @101
  SAY UNIDENTIFIED_DESC @201
  LPF ALTER_EFFECT INT_VAR match_opcode=321 STR_VAR resource=~%s1%~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=248 STR_VAR resource=~%s1%~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END


//Add Sever
COPY ~%icons%/sever.BAM~ ~override/%s2%.BAM~
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@102) STR_VAR bam=~%s2%~ RET icon END
COPY ~%abilities%/severvisual.EFF~    ~override/%s2%a.EFF~
COPY ~%abilities%/severdeath.EFF~     ~override/%s2%b.EFF~
COPY ~%abilities%/severslashing.EFF~  ~override/%s2%c.EFF~
COPY ~%abilities%/severremove.EFF~    ~override/%s2%d.EFF~
  WRITE_ASCII 0x30 ~%s2%e~ #8
COPY ~%abilities%/severremove.SPL~  ~override/%s2%e.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=321 STR_VAR resource=~%s2%~ END
  
COPY ~%abilities%/sever.SPL~        ~override/%s2%.SPL~
  SAY NAME1 @102
  SAY UNIDENTIFIED_DESC @202
  LPF ALTER_EFFECT INT_VAR match_opcode=248 STR_VAR match_resource=~spcl820~ resource=~%s2%a~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=248 STR_VAR match_resource=~quivvis~ resource=~%s2%b~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=248 STR_VAR match_resource=~archhit~ resource=~%s2%c~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=248 STR_VAR match_resource=~archmov~ resource=~%s2%d~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END


//Clone base Monk 2da file
COPY_EXISTING ~CLABMO01.2DA~ ~override/%kitid%.2DA~
  REPLACE_TEXTUALLY ~GA_SPCL811~ ~****~    // stunning blow
  REPLACE_TEXTUALLY ~GA_SPCL820~ ~****~    // quivering palm 
  REPLACE_TEXTUALLY ~GA_QDMNKA1~ ~****~    // stunning blows (monastic orders pre-0.42)
  REPLACE_TEXTUALLY ~GA_QDMNKA4~ ~****~    // quivering palm  (monastic orders pre-0.42)
  LPF d2_kit_ability_adder INT_VAR level=4 passive=1 multi=1 increment=4 STR_VAR resource=~%e1%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e2%~ END
  LPF d2_kit_ability_adder INT_VAR level=2 multi=1 increment=4 STR_VAR resource=~%s1%~ END
  LPF d2_kit_ability_adder INT_VAR level=13 STR_VAR resource=~%s2%~ END
BUT_ONLY


//Re-enable Monk class (if disabled by a mod)
COPY_EXISTING ~clsrcreq.2DA~  ~override~
  COUNT_2DA_ROWS 3 clsrcreq_rows
  READ_2DA_ENTRIES_NOW ~clsrcreq_table~ 3
  FOR (r = 1; r < clsrcreq_rows; ++r) BEGIN
    READ_2DA_ENTRY_FORMER ~clsrcreq_table~ r 0 class
    PATCH_IF (~%class%~ STRING_EQUAL_CASE ~MONK~) BEGIN
      SET_2DA_ENTRY r 1 3 1
    END
  END
BUT_ONLY


//Kit function
LAF ADD_KIT_EX
  INT_VAR
    kit_class = 20
    mixed = RESOLVE_STR_REF (@10201)
    lower = RESOLVE_STR_REF (@10202)
    help = RESOLVE_STR_REF (@10203)
  STR_VAR
    kit_name = ~D2MONASTERY~
    clab_path = ~%kitid%~
    clsrcreq = ~1 1 1 1 1 1 1~    // Usable by all races, if base class is usable
    weapprof = ~0 4 0 0 4 0 0 4 0 4 4 0 0 4 4 4 0 4 0 0 0 0 0 0 0 0 0 0 0 0 2 3~
    clswpbon = ~1 0 2~            // middle number is levels per fist 1/2 apr bonus
  RET 
    kit_id
END


//Make abilities unaffected by casting speed changes
ACTION_FOR_EACH ~ability~ IN ~%s1%~ ~%s2%~ BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~%ability%.SPL~) BEGIN
    COPY_EXISTING ~%ability%.SPL~  ~override~
      READ_SHORT 0x84 speed
      LPF DELETE_EFFECT INT_VAR check_headers=0 match_opcode=189 match_timing=0 END
      LPF ADD_SPELL_CFEFFECT INT_VAR opcode=189 target=1 parameter1=~%speed%~ parameter2=1 resist_dispel=2 duration=1 END
    BUT_ONLY
  END
END


/*
*/