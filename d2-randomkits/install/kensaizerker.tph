//Kensaizerker (d2mk1)

//Copy passive effects
COPY ~%passives%/hitdamage1.SPL~        ~override/%e1%.SPL~
COPY ~%passives%/speedfactorkensai.SPL~ ~override/%e2%.SPL~
COPY ~%passives%/acbonus2.SPL~          ~override/%e3%.SPL~ 


//Check for Enrage file
ACTION_IF (FILE_EXISTS_IN_GAME ~spcl321.SPL~) BEGIN
  COPY_EXISTING - ~spcl321.SPL~ ~override~
    READ_STRREF 0x8 ragename
END
  
ACTION_IF (~%ragename%~ STRING_EQUAL_CASE ~Enrage~) BEGIN
  COPY_EXISTING ~SPCL321.SPL~  ~override/%s1%.SPL~    // clone existing Enrage
  COPY_EXISTING ~SPCL321D.SPL~ ~override/%s2%.SPL~
END
ELSE BEGIN
  COPY ~%abilities%/enrage.SPL~ ~override/%s1%.SPL~    // or add new files
  COPY ~%abilities%/winded.SPL~ ~override/%s2%.SPL~
END

  
//Update some stuff on Enrage/winded files
COPY_EXISTING ~%s1%.SPL~ ~override~
  SAY NAME1 @100
  SAY UNIDENTIFIED_DESC @200
  LPF ALTER_EFFECT INT_VAR match_opcode=146 STR_VAR match_resource=~SPCL321D~ resource=~%s2%~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=RESOLVE_STR_REF (@300) END
  
  PATCH_IF (GAME_IS ~bgee bg2ee iwdee eet~) BEGIN
    LPF DELETE_EFFECT INT_VAR match_opcode=206 STR_VAR match_resource=~SPCL321~ END
    LPF ADD_SPELL_EFFECT INT_VAR opcode=321 target=1 timing=1 resist_dispel=2 insert_point=0 STR_VAR resource=~%s2%~ END
    LPF ADD_SPELL_EFFECT INT_VAR opcode=321 target=1 timing=1 resist_dispel=2 insert_point=0 STR_VAR resource=~%s1%~ END
  END
  ELSE BEGIN
    LPF ALTER_EFFECT INT_VAR match_opcode=206 parameter1=RESOLVE_STR_REF (@302) STR_VAR match_resource=~SPCL321~ resource=~%s1%~ END
  END
BUT_ONLY

COPY_EXISTING ~%s2%.SPL~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=RESOLVE_STR_REF (@301) END  
BUT_ONLY


//Clone base Fighter 2da file
COPY_EXISTING ~CLABFI01.2DA~ ~override/%kitid%.2DA~
  LPF d2_kit_ability_adder INT_VAR multi=1 increment=4 STR_VAR resource=~%s1%~ END
  LPF d2_kit_ability_adder INT_VAR level=3 passive=1 multi=1 increment=3 STR_VAR resource=~%e1%~ END
  LPF d2_kit_ability_adder INT_VAR level=4 passive=1 multi=1 increment=4 STR_VAR resource=~%e2%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e2%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e3%~ END
BUT_ONLY


//Kit function
LAF ADD_KIT_EX
  INT_VAR
    mixed = RESOLVE_STR_REF (@10101)
    lower = RESOLVE_STR_REF (@10102)
    help = RESOLVE_STR_REF (@10103)
  STR_VAR
    kit_name = ~D2KENSAIZERKER~
    source_kit = ~KENSAI~
    alignmnt = ~0 0 0 1 1 1 1 1 1~
    clab_path = ~%kitid%~
  RET 
    kit_id
END




/*Alt version that checks existing 2da, then clones or makes new file

//Make 2da file
COPY_EXISTING - ~CLABFI04.2DA~ ~override~    // check if kensai was overhauled
  COUNT_REGEXP_INSTANCES ~AP_SPCL141~ d1
  COUNT_REGEXP_INSTANCES ~AP_SPCL142~ d2
  COUNT_REGEXP_INSTANCES ~AP_SPCL143~ d3
  COUNT_REGEXP_INSTANCES ~GA_SPCL144~ d4
  
ACTION_IF ((%d1%>0) AND (%d2%>0) AND (%d3%>0) AND (%d4%>0)) BEGIN    // copy kensai 2da
  COPY_EXISTING ~CLABFI04.2DA~ ~override/%kitid%.2DA~
    REPLACE_TEXTUALLY ~SPCL141~ ~%e1%~
    REPLACE_TEXTUALLY ~SPCL142~ ~%e3%~
    REPLACE_TEXTUALLY ~SPCL143~ ~%e2%~
    REPLACE_TEXTUALLY ~SPCL144~ ~%s1%~
    PRETTY_PRINT_2DA
  BUT_ONLY
END
ELSE BEGIN
  COPY ~%files%/kit0.2DA~ ~override/%kitid%.2DA~    // or make new 2da
    LPF d2_kit_ability_adder INT_VAR multi=1 increment=4 STR_VAR resource=~%s1%~ END
    LPF d2_kit_ability_adder INT_VAR level=3 passive=1 multi=1 increment=3 STR_VAR resource=~%e1%~ END
    LPF d2_kit_ability_adder INT_VAR level=4 passive=1 multi=1 increment=4 STR_VAR resource=~%e2%~ END
    LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e2%~ END
    LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e3%~ END
    LPF d2_kit_ability_adder END
  BUT_ONLY
END

*/