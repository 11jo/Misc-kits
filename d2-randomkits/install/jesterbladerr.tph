//Jesterblade (d2mk3)


//check luabbr
OUTER_SET jesterblade_hla = 0
COPY_EXISTING - ~luabbr.2da~  ~~
  LPF 2da_find_row STR_VAR row_name=~BLADE~ RET row_number columns END
  READ_2DA_ENTRY row_number 1 columns ~blade_hla~

  LPF 2da_find_row STR_VAR row_name=~JESTER~ RET row_number columns END
  READ_2DA_ENTRY row_number 1 columns ~jester_hla~

  PATCH_IF !(~%blade_hla%~ STR_EQ ~Ba0~) BEGIN
    SET jesterblade_hla = 1
  END


//variables
OUTER_TEXT_SPRINT blade     ~lu%blade_hla%~
OUTER_TEXT_SPRINT jester    ~lu%jester_hla%~
OUTER_TEXT_SPRINT hlatable  ~lu%kitid%~

ACTION_IF (RESOURCE_CONTAINS ~%jester%.2da~  ~SPCL920~) BEGIN  OUTER_SET jesterblade_hla = 0 END    // not if jester has vanilla HLA song
ACTION_IF (RESOURCE_CONTAINS ~%blade%.2da~  ~DW~)       BEGIN  OUTER_SET jesterblade_hla = 1 END    // enable if ToF
ACTION_IF (RESOURCE_CONTAINS ~%blade%.2da~  ~#BARD6~)   BEGIN  OUTER_SET jesterblade_hla = 1 END    // enable if IWD songs

//defaults to using same table as blade if NOT making custom table (unmodded Blade uses same table as Bard)


//make HLA table if revision detected
ACTION_IF (jesterblade_hla > 0) BEGIN

  //set 2da
  ACTION_IF (FILE_EXISTS_IN_GAME ~%blade%.2DA~) BEGIN
    COPY_EXISTING ~%blade%.2DA~  ~override/%hlatable%.2DA~
  END ELSE BEGIN
    COPY_EXISTING ~LUBA0.2DA~    ~override/%hlatable%.2DA~
  END
  
  COPY_EXISTING ~luabbr.2da~  ~override~
    LPF 2da_find_row_set STR_VAR row_name=~D2%kitname%~ entry=~%kitid%~ END
  BUT_ONLY

END


//
//rr - add jester passives
ACTION_IF (RESOURCE_CONTAINS ~CLABBA03.2da~ ~RR#BJSIT~) BEGIN
  ACTION_IF !(RESOURCE_CONTAINS ~%kitid%.2da~ ~RR#BJSIT~) BEGIN
    COPY_EXISTING ~%kitid%.2DA~  ~override~
      LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~RR#BJSIT~ END
    BUT_ONLY
  END
END

//rr - hlas
ACTION_IF (jesterblade_hla > 0) BEGIN
  ACTION_IF (RESOURCE_CONTAINS ~%jester%.2da~ ~RR#BJS01~) BEGIN

    // get lines from jester table
    COPY_EXISTING - ~%jester%.2da~  ~~
      LPF d2_get_hla_string STR_VAR clone_res=~AP_RR#BJS01~ RET hlastring END
      TEXT_SPRINT rr_enhanced  ~%hlastring%~
      LPF d2_get_hla_string STR_VAR clone_res=~AP_RR#BJS03~ RET hlastring END
      TEXT_SPRINT rr_lingering  ~%hlastring%~

    // add to hla table
    COPY_EXISTING ~%hlatable%.2DA~  ~override~
      LPF d2_add_hla_string STR_VAR hlastring=~%rr_enhanced%~ END
      LPF d2_add_hla_string STR_VAR hlastring=~%rr_lingering%~ END
    BUT_ONLY
  END
END


//
//Shohy bard mod
ACTION_IF (RESOURCE_CONTAINS ~CLABBA02.2da~ ~BI#B20A~) BEGIN

  // edit jester passives
  COPY_EXISTING ~BI#B30A.spl~  ~override/d2BIjst.SPL~
    LPF DELETE_EFFECT INT_VAR match_opcode=251 END
    LPF DELETE_EFFECT INT_VAR match_opcode=146 STR_VAR match_resource=~BI#B00D~ END    // remove choir abilities
  IF_EXISTS

  // add jester passives to kit table (already has blade stuff)
  ACTION_IF !(RESOURCE_CONTAINS ~%kitid%.2da~ ~d2BIjst~) BEGIN
    COPY_EXISTING ~%kitid%.2DA~  ~override~
      LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~d2BIjst~ END
    BUT_ONLY
  END

  // change hla song to jester song
  COPY_EXISTING ~%hlatable%.2DA~  ~override~
    REPLACE_TEXTUALLY EXACT_MATCH ~AP_BI#B2SS~  ~AP_BI#B3SS~
    PRETTY_PRINT_2DA
  IF_EXISTS BUT_ONLY

  // give "chorus" abilities if HLA song is taken
  COPY_EXISTING ~BI#B3SS.spl~  ~override~
    LPF CLONE_EFFECT
      INT_VAR match_opcode=251 opcode=326 parameter2=109 parameter1=IDS_OF_SYMBOL (kit ~D2%kitname%~)
      STR_VAR resource=~BI#B00D~ END
  IF_EXISTS BUT_ONLY
END


//
//Bardic Wonders - add Heckle ability
ACTION_IF (RESOURCE_CONTAINS ~CLABBA03.2da~ ~C0JES01~) BEGIN
  ACTION_IF !(RESOURCE_CONTAINS ~%kitid%.2da~ ~C0JES01~) BEGIN
    COPY_EXISTING ~%kitid%.2DA~  ~override~
      LPF d2_kit_ability_adder INT_VAR multi=1 increment=1 STR_VAR resource=~C0JES01~ END
    BUT_ONLY
  END
END

//add magic flute HLA if missing
ACTION_IF (RESOURCE_CONTAINS ~%jester%.2da~ ~GA_SPCL921~) BEGIN
  ACTION_IF !(RESOURCE_CONTAINS ~%hlatable%.2da~ ~GA_SPCL921~) BEGIN
    LAF d2_get_hla_string STR_VAR clone_res=GA_SPCL921 clone_2da=~%jester%~ RET hlastring END
    LAF d2_add_hla_string STR_VAR hlatable=~%hlatable%~ hlastring=~%hlastring%~ END
  END
END


//bardic wonders - song tweaks
OUTER_TEXT_SPRINT bardsong  ~%s1%~

ACTION_IF ((FILE_EXISTS ~override/c0bsngef.vvc~) AND
           (FILE_EXISTS ~override/c0bsngef.bam~)
) BEGIN
  COPY ~override/%bardsong%.spl~  ~override~
    LPF CLONE_EFFECT
      INT_VAR match_opcode=215 target=1 parameter2=1 timing=10 duration=7 savingthrow=0 savebonus=0
      STR_VAR resource=c0bsngef insert=first
    END
  BUT_ONLY
END

ACTION_IF ((FILE_EXISTS ~override/c0bardsx.spl~) AND
           (FILE_EXISTS ~override/c0singi2.spl~) AND
           (FILE_EXISTS ~override/SPCL751A.eff~) AND
           (FILE_EXISTS ~override/SPCL751A.spl~)
) BEGIN

  // adjust file structure
  COPY ~override/%bardsong%.spl~  ~override/%bardsong%s.SPL~
    LPF ALTER_EFFECT INT_VAR silent=1 STR_VAR match_resource=~%SOURCE_RES%~ resource=~%DEST_RES%~ END
  COPY ~override/SPCL751A.spl~    ~override/%bardsong%.SPL~
    LPF ALTER_EFFECT INT_VAR silent=1 STR_VAR match_resource=~%SOURCE_RES%~ resource=~%DEST_RES%~ END
  COPY ~override/SPCL751A.eff~    ~override/%bardsong%.eff~
    WRITE_ASCII 0x30 ~%bardsong%s~ #8

  // update kit desc
  OUTER_SPRINT bardic_tweak @650
  ACTION_GET_STRREF jesterblade_desc jesterblade_text
  OUTER_TEXT_SPRINT jesterblade_text ~%jesterblade_text%%WNL%%WNL%%bardic_tweak%~
  STRING_SET_EVALUATE jesterblade_desc ~%jesterblade_text%~

END


//
//IWDification (jester/skald song tweak)
ACTION_IF (RESOURCE_CONTAINS ~CLABBA03.2da~ ~cdcl751~) BEGIN
  COPY ~override/cdcl751.spl~  ~override/%m1%.SPL~
    SAY NAME1 @105
    SAY UNIDENTIFIED_DESC @205
    LPF ALTER_EFFECT INT_VAR match_opcode=251 STR_VAR resource=~%s1%~ END
    LPF ALTER_EFFECT INT_VAR match_opcode=171 STR_VAR resource=~%DEST_RES%~ END
    LPF ALTER_EFFECT INT_VAR match_opcode=172 STR_VAR resource=~%DEST_RES%~ END

    // tweak casting
    LPF ALTER_SPELL_HEADER INT_VAR target=7 END
    LPF DELETE_EFFECT INT_VAR match_opcode=174 END
    LPF CLONE_EFFECT INT_VAR match_opcode=251 opcode=174 timing=1 STR_VAR resource=EFF_M02 END
  IF_EXISTS

  // add to kit clab
  ACTION_IF (FILE_EXISTS ~override/%m1%.SPL~) BEGIN
    ACTION_IF !(RESOURCE_CONTAINS ~%kitid%.2da~ ~%m1%~) BEGIN
      COPY_EXISTING ~%kitid%.2DA~  ~override~
        LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%m1%~ END
      BUT_ONLY
    END
  END
END


//
//if ToF selectable Jester song detected
ACTION_IF (RESOURCE_CONTAINS ~CLABBA03.2da~ ~dw-sng2~) BEGIN
  COPY ~override/dw-sng2.spl~  ~override/%m1%.SPL~
    SAY NAME1 @105
    SAY UNIDENTIFIED_DESC @205
    LPF ALTER_EFFECT INT_VAR match_opcode=251 STR_VAR resource=~%s1%~ END
    LPF ALTER_EFFECT INT_VAR match_opcode=171 STR_VAR resource=~%DEST_RES%~ END
    LPF ALTER_EFFECT INT_VAR match_opcode=172 STR_VAR resource=~%DEST_RES%~ END

    // tweak casting
    LPF ALTER_SPELL_HEADER INT_VAR target=7 END
    LPF DELETE_EFFECT INT_VAR match_opcode=174 END
    LPF CLONE_EFFECT INT_VAR match_opcode=251 opcode=174 timing=1 STR_VAR resource=EFF_M02 END
  IF_EXISTS

  // add to kit clab
  ACTION_IF (FILE_EXISTS ~override/%m1%.SPL~) BEGIN
    ACTION_IF !(RESOURCE_CONTAINS ~%kitid%.2da~ ~%m1%~) BEGIN
      COPY_EXISTING ~%kitid%.2DA~  ~override~
        LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%m1%~ END
      BUT_ONLY
    END
  END
END


//
//if ToF HLAs detected (experimental, some HLAs not selectable)
ACTION_IF (jesterblade_hla > 0) BEGIN
  ACTION_IF ((RESOURCE_CONTAINS ~%blade%.2da~ ~#BARD6~) OR
             (RESOURCE_CONTAINS ~%jester%.2da~ ~HLJES~)
  ) BEGIN

    // get info
    ACTION_IF (FILE_EXISTS ~override/DW-HLJES.spl~) BEGIN
      ACTION_IF (RESOURCE_CONTAINS ~%jester%.2da~ ~DW-HLJES~) BEGIN
        LAF d2_get_hla_string STR_VAR clone_res=~AP_DW-HLJES~ clone_2da=~%jester%~ RET hlastring END
        OUTER_SET tof_jesterhla = 1
      END
    END

    // patch hla table
    COPY ~override/%hlatable%.2da~  ~override~
      PATCH_IF (IS_AN_INT tof_jesterhla AND tof_jesterhla > 0) BEGIN
        LPF d2_add_hla_string INT_VAR overwrite=1 STR_VAR hlastring=~%hlastring%~ match=~AP_SPCL920~ END
      END

//    PATCH_IF !(RESOURCE_CONTAINS ~%kitid%.2da~ ~%m1%~) BEGIN
        PATCH_IF (RESOURCE_CONTAINS ~%blade%.2da~  ~#BARD6~) BEGIN
          PATCH_FOR_EACH song IN ~#BARD2~ ~#BARD3~ ~#BARD4~ ~#BARD5~ ~#BARD6~ BEGIN
            PATCH_IF (RESOURCE_CONTAINS ~%hlatable%.2da~ ~%song%~) BEGIN
              LPF 2da_find_row_delete INT_VAR col_read=1 col_count=3 STR_VAR row_name=~GA_%song%~ END
            END
          END
          LPF d2_add_hla_string INT_VAR renumber=1 END
        END
//    END
    BUT_ONLY
  END
END


//
//give ToF ambidexterity (not selectable)
ACTION_IF (RESOURCE_CONTAINS ~%blade%.2da~  ~dw-ftamb~) BEGIN
  COPY ~%abilities%/applyeffects.spl~  ~override/%m5%.spl~
    LPF ALTER_EFFECT STR_VAR resource=~dw-ftamb~ END
    LPF CLONE_EFFECT INT_VAR opcode=313 timing=9 END
END

ACTION_IF (FILE_EXISTS ~override/%m5%.spl~) BEGIN
  COPY ~override/%kitid%.2DA~  ~override~
    REPLACE_TEXTUALLY ~AP_%m5%~  ~****~
    LPF d2_kit_ability_adder INT_VAR level=20 passive=1 STR_VAR resource=~%m5%~ END
  BUT_ONLY
END


//variables
OUTER_TEXT_SPRINT heckle      ~C0JES01~


//dummy files
COPY_EXISTING - ~SPCL751.spl~  ~inlined/%temp%.SPL~
  WRITE_LONG 0x8 ~-1~

ACTION_FOR_EACH spell IN
  ~d2BIjst~
  ~%bardsong%s~
  ~%m1%~
  ~%m5%~
BEGIN
  ACTION_IF !(FILE_EXISTS ~override/%file%~) BEGIN
    COPY ~inlined/%temp%.SPL~  ~override/%spell%.SPL~
  END
END

ACTION_IF !(FILE_EXISTS ~override/%hlatable%.2DA~) BEGIN
  COPY_EXISTING ~LUBA0.2DA~  ~override/%hlatable%.2DA~
END
ACTION_IF !(FILE_EXISTS ~override/%bardsong%.eff~) BEGIN
  COPY_EXISTING ~archhit.eff~  ~override/%bardsong%.EFF~
END


//
//